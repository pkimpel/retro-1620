3400032007013600032007024902402511963611300102 COLD START CARD - 1620 MONITOR I
||JOB                        PLOT CIRCLE PROTOTYPE
||DUP                        WILL HALT ON FIRST USE BECAUSE CIRCLE ISNT THERE
*DELETCIRCLE
||FOR 5   |
*LIST PRINTER
*LDISKCIRCLE
C-----------------------------------------------------------------------
C CIRCLE DRAWING PROTOTYPE
C ADAPTED FROM -A LINEAR ALGORITHM FOR INCREMENTAL DIGITAL DISPLAY OF
C ARCS-, JACK BRESENHAM, CACM VOL 20, NUMBER 2, FEBRUARY 1977, PAGE 100.
C
C THE RADIUS PARAMETER IS THE RADIUS OF THE CIRCLE IN INCHES. THE
C ROUTINE ASSUMES THE CENTER OF THE CIRCLE IS AT THE CURRENT PEN
C POSITION, AND RETURNS TO THAT POSITION WITH THE PEN UP AFTER THE
C CIRCLE IS DRAWN.
C
C 2024-01-30  P.KIMPEL
C   ORIGINAL VERSION.
C-----------------------------------------------------------------------
C
      SUBROUTINE CIRCLE(RADIUS)
C
C     -- CONVERT THE RADIUS FROM INCHES TO 0.01-INCH PLOTTER STEPS
      IR = RADIUS*100.0 + 0.5
C     -- RAISE THE PEN
      IM = 9
      PUNCH TAPE 99, IM
   99 FORMAT(I1)
C
C     -- STEP PEN FROM THE CENTER TO THE RADIUS
      MS = 3
      DO 100 I=1,IR
        PUNCH TAPE 99, MS
  100   CONTINUE
      MS = MS+2
      IF (MS-9) 120, 110, 110
  110 MS = 1
C
C     -- PEN DOWN
  120 CONTINUE
      IM = 0
      PUNCH TAPE 99, IM
      M3 = MS
C
C     -- QUADRANT LOOP
  200 CONTINUE
      TYPE 202, M3
  202 FORMAT(19HSTART QUADRANT, M3=,I1)
      IX = 1
      IY = IR+IR-1
      ID = IY-1
      M1 = M3
      M2 = M1+1
      M3 = M2+1
      IF (M3-9) 300, 210, 300
  210 M3 = 1
C
C     -- PLOTTER STEP LOOP
  300 CONTINUE
      IF (IY) 900, 310, 310
  310 CONTINUE
      ID2 = ID+ID
      IF (ID2) 400, 320, 320
  320 CONTINUE
      ID2 = ID2-IY
      IF (ID2) 500, 330, 330
  330 CONTINUE
      IX = IX+2
      ID = ID-IX
      IM = M1
      GO TO 800
C
  400 CONTINUE
      ID2 = ID2+IX
      IF (ID2) 410, 500, 500
  410 CONTINUE
      IY = IY-2
      ID = ID+IY
      IM = M3
      GO TO 800
C
  500 CONTINUE
      IX = IX+2
      IY = IY-2
      ID = ID+IY-IX
      IM = M2
C
C     -- PLOTTER MOVE
  800 CONTINUE
      PUNCH TAPE 99, IM
      GO TO 300
C
C     -- END OF QUADRANT
  900 CONTINUE
      IF (M3-MS) 200, 910, 200
C
C     -- END OF CIRCLE, PEN UP AND RETURN TO CENTER
  910 CONTINUE
      IM = 9
      PUNCH TAPE 99, IM
      TYPE 912, M3
  912 FORMAT(18HEND OF CIRCLE, M3=,I1)
      IM = M3+2
      IF (IM-9) 930, 920, 920
  920 IM = 1
  930 CONTINUE
      DO 950 I=1,IR
        PUNCH TAPE 99, IM
  950 CONTINUE
      RETURN
      END
||||              END OF JOB
