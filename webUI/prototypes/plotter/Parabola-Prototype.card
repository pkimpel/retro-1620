3400032007013600032007024902402511963611300102 COLD START CARD - 1620 MONITOR I
||JOB                        PLOT PARABOLA PROTOTYPE
||DUP                        WILL HALT ON FIRST USE BECAUSE PARAB ISNT THERE
*DELETPARAB
||FOR 5   |
*LIST PRINTER
*LDISKPARAB
C-----------------------------------------------------------------------
C PARABOLA DRAWING SUBROUTINE
C
C ADAPTED FROM --
C EFFICIENT INTEGER ALGORITHMS FOR THE GENERATION OF CONIC SECTIONS
C   A. AGATHOS, T. THEOHARIS AND A. BOEHM
C   DEPARTMENT OF INFORMATICS, UNIVERSITY OF ATHENS,
C   PANEPISTIMIOUPOLIS, TYPA BUILDINGS, 157 71 ATHENS, GREECE
C   COMPUT. + GRAPHICS, VOL. 22. NO. 5, PP. 621-628. 1998
C
C THE VERTEX (THE /NOSE/ OF THE PARABOLA) IS ASSUMED TO BE AT THE
C CURRENT PEN LOCATION. (XB,YB) ARE THE SIGNED OFFSETS FOR EITHER OF
C BOUNDS (THE TAIL ENDS OF THE PARABOLA) FROM THE VERTEX. THESE OFFSETS
C MUST BE EXPRESSED IN /INCHES/. IF THE VALUES OF BOTH XB AND YB ARE
C ZERO, THE PARABOLA HAS NO SIZE, AND THE ROUTINE SIMPLY EXITS.
C
C ONE LEG OF THE PARABOLA WILL BE DRAWN FROM VERTEX TO TAIL, THE PEN
C WILL RAISE AND RETURN TO THE VERTEX, THEN THE OTHER TAIL WILL BE
C DRAWN, AND FINALLY THE PEN WILL RAISE AND RETURN TO THE VERTEX, IN
C ITS  ORIGINAL POSITION.
C
C THE SIGNS OF XB AND YB DETERMINE THE PARABOLA ORIENTATION, THUS --
C   +XB,+YB  PARABOLA OPEN END IS TOWARDS +X
C   +XB,-YB  PARABOLA OPEN END IS TOWARDS -Y
C   -XB,-YB  PARABOLA OPEN END IS TOWARDS -X
C   -XB,+YB  PARABOLA OPEN END IS TOWARDS +Y
C
C VARIABLES --
C   ID    CURRENT ERROR TERM
C   IH    INDICATES HALF-PARABOLAS ALREADY DRAWN, 0/1
C   IM    PLOTTER COMMAND, 1-8 FOR STEPPING, 0,9 FOR PEN UP/DOWN
C   IP    INTEGERIZED P FACTOR (TWICE THE PARABOLA FOCAL LENGTH)
C   IP2   IP*2
C   IP4   IP*4
C   IR    CURRENT PARABOLA REGION, 1=Y LSS P, 2=Y GEQ P, 3=RTN TO VERTEX
C   IRXY  PARABOLA IS ROTATED, SO DIRECTIONS WILL BE SWAPPED, 0/1
C   ITPD  TEMP FOR SWAPPING PEN DIRECTIONS
C   IX    CURRENT X COORDINATE
C   IXB   INTEGERIZED X COORDINATE BOUND
C   IXD   X STEPPING DIRECTION, +1/-1
C   IXPD  CURRENT X PEN DIRECTION, +1/-1
C   IXPR  NUMBER OF X STEPS TO RETURN PEN TO VERTEX
C   IY    CURRENT Y COORDINATE
C   IYB   INTEGERIZED Y COORDINATE BOUND -- NOT USED --
C   IYD   Y STEPPING DIRECTION, +1/-1
C   IYPD  CURRENT Y PEN DIRECTION, +1/-1
C   IYPR  NUMBER OF Y STEPS TO RETURN PEN TO VERTEX
C   X     ABS(XB) CONVERTED FROM INCHES TO 0.01-INCH PLOTTER STEPS
C   Y     ABS(YB) CONVERTED FROM INCHES TO 0.01-INCH PLOTTER STEPS
C
C 2024-02-04  P.KIMPEL
C   ORIGINAL VERSION.
C-----------------------------------------------------------------------
C
      SUBROUTINE PARAB(XB, YB)
C
C     -- CHECK IF PARABOLA IS ROTATED AND DIRECTIONS NEED TO BE SWAPPED
C     -- IF XB=YB=0, THE PARABOLA HAS NO SIZE, SO EXIT WITHOUT DRAWING
      IRXY = 0
      X = XB
      Y = YB
      IF (XB) 32, 34, 34
   32 IF (YB) 50, 36, 36
   34 IF (YB) 36, 590, 50
   36 IRXY = 1
      X = YB
      Y = XB
C
C     -- DETERMINE PARABOLA ORIENTATION
   50 IXD = 1
      IYD = 1
      IF (X) 52, 54, 54
   52 IXD = -1
   54 IF (Y) 56, 80, 80
   56 IYD = -1
C
C     -- CONVERT (X, Y) FROM INCHES TO 0.01-INCH PLOTTER STEPS
   80 CONTINUE
      X = ABS(X)*100.0
      Y = ABS(Y)*100.0
      IXB = X + 0.5
C--   IYB = Y + 0.5     -- NOT USED --
C
C     -- INITIALIZE THE STEPPING ALGORITHM
      IP = Y*Y/X/2.0 + 0.5
      IP2 = IP+IP
      IP4 = IP2+IP2
      IH = 0
C
C     -- INITIALIZE THE HALF-PARABOLA LOOP
  100 CONTINUE
      IX = 0
      IY = 0
      ID = 1-IP
      IR = 1
      IXPD = 0
      IXPR = 0
      IYPD = 0
      IYPR = 0
CD    PRINT 66
CD 66 FORMAT(55H IH IR    IX    IY  IXPR  IYPR    ID IXD IYD IXPD IYPD ,
CD   1       12HIM   IP IRXY)
C
C     -- LOWER THE PEN
      IM = 0
      PUNCH TAPE 99, IM
   99 FORMAT(I1)
C
C DRAW THE CURRENT POINT - FIRST TEST IF THE BOUND WAS REACHED
  200 CONTINUE
      IF (IX-IXB) 210, 210, 500
C
C     -- IN BOUNDS, SO CHECK IF ROTATED AND NEED TO SWAP STEPS
  210 CONTINUE
      IF (IRXY) 220, 230, 220
  220 ITPD = IXPD
      IXPD = IYPD
      IYPD = ITPD
C
C     -- DETERMINE THE PLOTTER COMMAND FROM STEP DIRECTIONS
C     -- IF THERE IS NO PEN MOTION (UNLIKELY), BYPASS OUTPUT
  230 CONTINUE
      IF (IXPD) 240, 250, 260
  240 IXPR = IXPR+1
      IF (IYPD) 242, 244, 246
  242 IM = 6
      IYPR = IYPR+1
      GO TO 280
  244 IM = 7
      GO TO 280
  246 IM = 8
      IYPR = IYPR+1
      GO TO 280
C
  250 IF (IYPD) 252, 290, 256
  252 IM = 5
      IYPR = IYPR+1
      GO TO 280
  256 IM = 1
      IYPR = IYPR+1
      GO TO 280
C
  260 IXPR = IXPR+1
      IF (IYPD) 262, 264, 266
  262 IM = 4
      IYPR = IYPR+1
      GO TO 280
  264 IM = 3
      GO TO 280
  266 IM = 2
      IYPR = IYPR+1
C
C     -- OUTPUT THE PLOTTER COMMAND
  280 PUNCH TAPE 99, IM
C
C     -- GO (OR RETURN) TO THE APPROPRIATE REGION OF THE PLOT
  290 CONTINUE
CD    PRINT 77, IH, IR, IX, IY, IXPR, IYPR, ID, IXD, IYD, IXPD, IYPD,
CD   1          IM, IP, IRXY
CD 77 FORMAT(2I3,5I6,2I4,2I5,I3,2I5)
      GO TO (300, 400, 510), IR
C
C     -- STEPPING FOR REGION 1, Y LESS P
  300 CONTINUE
      IXPD = 0
      IF (IY-IP) 310, 390, 390
  310 IF (ID) 330, 320, 320
  320 IX = IX+1
      ID = ID-IP2
      IXPD = IXD
  330 IY = IY+1
      ID = ID+IY*2+1
      IYPD = IYD
      GO TO 200
C
C     -- SWITCH TO REGION 2
  390 CONTINUE
      IR = 2
      IF (ID-1) 394, 392, 394
  392 ID = 1-IP4
      GO TO 200
  394 ID = 1-IP2
      GO TO 200
C
C     -- STEPPING FOR REGION 2, Y GEQ P
  400 CONTINUE
      IYPD = 0
      IF (ID) 410, 410, 420
  410 IY = IY+1
      ID = ID+IY*4
      IYPD = IYD
  420 IX = IX+1
      ID = ID-IP4
      IXPD = IXD
      GO TO 200
C
C     -- FINISH A HALF PARABOLA, RAISE PEN AND RETURN TO VERTEX
  500 CONTINUE
      IM = 9
      PUNCH TAPE 99, IM
      IR = 3
      IF (IRXY) 502, 502, 504
  502 IX = IXPR
      IY = IYPR
      GO TO 510
  504 IX = IYPR
      IY = IXPR
C
C     -- STEPPING FOR RETURN TO VERTEX PSEUDO-REGION
  510 CONTINUE
      IYPD = 0
      IF (IY) 512, 512, 520
  512 IF (IX) 550, 550, 530
  520 IY = IY-1
      IYPD = -IYD
  530 IXPD = 0
      IF (IX) 210, 210, 540
  540 IX = IX-1
      IXPD = -IXD
      GO TO 210
C
C     -- DETERMINE IF BOTH HALVES HAVE BEEN DRAWN
  550 CONTINUE
      IF (IH) 560, 560, 590
  560 IH = 1
      IYD = -IYD
      GO TO 100
  590 RETURN
      END
||||              END OF JOB
